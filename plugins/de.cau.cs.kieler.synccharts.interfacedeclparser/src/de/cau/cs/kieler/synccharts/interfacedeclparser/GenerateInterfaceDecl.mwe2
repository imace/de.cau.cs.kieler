module de.cau.cs.kieler.synccharts.interfacedeclparser.GenerateInterfaceDecl

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*

var grammarURI = "classpath:/de/cau/cs/kieler/synccharts/interfacedeclparser/InterfaceDecl.xtext"
var file.extensions = "ifd"
var projectName = "de.cau.cs.kieler.synccharts.interfacedeclparser"
var projectNameUI = "de.cau.cs.kieler.synccharts.interfacedeclparser.ui"
var runtimeProject = "../${projectName}" 
var runtimeProjectUI = "../${projectNameUI}"

Workflow {
	 bean = StandaloneSetup { 
		platformUri = "${runtimeProject}/.."
		registerGeneratedEPackage = "de.cau.cs.kieler.synccharts.SyncchartsPackage"
		registerGeneratedEPackage = "de.cau.cs.kieler.expressions.ExpressionsPackage"
		registerGeneratedEPackage = "de.cau.cs.kieler.annotations.AnnotationsPackage" 
	}
	
	component = DirectoryCleaner {
		directory="${runtimeProject}/src-gen"
		directory="${runtimeProject}.ui/src-gen"
	}
	
	component = Generator {
		pathRtProject = runtimeProject
		pathUiProject = runtimeProjectUI
		projectNameRt = projectName
		projectNameUi = projectNameUI
		
		language = {
			uri=grammarURI
			fileExtensions=file.extensions
			fragment = grammarAccess.GrammarAccessFragment {}
		/*	fragment = ecore.EcoreGeneratorFragment {
				genModels = "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.genmodel"
				genModels = "platform:/resource/de.cau.cs.kieler.core.expressions/model/expressions.genmodel"
				genModels = "platform:/resource/de.cau.cs.kieler.core.annotations/model/annotations.genmodel"
			}*/
			
			fragment = ecore.EcoreGeneratorFragment {
			   referencedGenModels = "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.genmodel"//, platform:/resource/de.cau.cs.kieler.core.expressions/model/expressions.genmodel, platform:/resource/de.cau.cs.kieler.core.annotations/model/annotations.genmodel"
			}
			
			fragment = parseTreeConstructor.ParseTreeConstructorFragment {}
			fragment = resourceFactory.ResourceFactoryFragment {
				fileExtensions = file.extensions
			}	
			fragment = AntlrDelegatingFragment {}
			fragment = validation.JavaValidatorFragment {
				composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			fragment = scoping.ImportNamespacesScopingFragment {}
			fragment = exporting.QualifiedNamesFragment{}
			fragment = formatting.FormatterFragment {}
			fragment = labeling.LabelProviderFragment {}
			fragment = outline.TransformerFragment {}
			fragment = outline.OutlineNodeAdapterFactoryFragment {}
			fragment = contentAssist.JavaBasedContentAssistFragment {}
			fragment = DelegatingGeneratorFragment {
				delegate="de.itemis.xtext.antlr.XtextAntlrUiGeneratorFragment"
				message="You are generating without ANTLR. It is highly recommended to download and use the plugin 'de.itemis.xtext.antlr' \n\t using the update site http://download.itemis.com/updates/."			
			}	
			fragment = builder.BuilderIntegrationFragment {}
		}
	}
}