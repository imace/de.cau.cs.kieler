/*
 * KIELER - Kiel Integrated Environment for Layout Eclipse RichClient
 *
 * http://www.informatik.uni-kiel.de/rtsys/kieler/
 * 
 * Copyright 2010 by
 * + Christian-Albrechts-University of Kiel
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 * 
 *****************************************************************************/
grammar de.cau.cs.kieler.synccharts.interfacedeclparser.InterfaceDecl with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.ecore" as synccharts

generate interfaceDecl "http://www.cau.de/cs/kieler/synccharts/interfacedeclparser/InterfaceDecl"
 
 
//==============================================================================
// "Buckets" containing the Signals / Regions with Signals
//==============================================================================

StateExtend:
	(
	(regions+=RegionSignalDec) |	
	("input" "output" (inOutputSignals+=InOutputSignals))  |
	("output" (outputSignals+=OutputSignals)) |
	("input" (inputSignals+=InputSignals)) |
	("signal" (signals+=Signals)) |
	("renaming" (renamings+=Renamings)) 	
	)+
	;

RegionSignalDec:
	region = [synccharts::Region|ID] ":" (
	("var" vars+=Variable ("," (vars+=Variable))*) |
	("signal" signals+=Signal ("," (signals+=Signal))*) 	
	)
	(
	("," "var" vars+=Variable ("," (vars+=Variable))*) |
	("," "signal" signals+=Signal ("," (signals+=Signal))*) 	
	)* 
	";"
	;
	
Variables:
    (vars+=Variable ("," (vars+=Variable))*) ";"
    ;

Signals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;
	
Renamings:
    (renamings+=Renaming ("," (renamings+=Renaming))*) ";"
    ;
	
InputSignals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

OutputSignals: 
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

InOutputSignals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

//==============================================================================
// synccharts elements as needed
//==============================================================================

Signal returns synccharts::Signal:
 
	(
	name=ID |
	name=ID ":" (type=ValueType) |
	name=ID (":=" initialValue=STRING_D) |
	name=ID (":=" initialValue=STRING_D) ":" (type=ValueType) |
	name=ID (":=" initialValue=STRING_D) ("combine" (type=ValueType) "with" (combineOperator=CombineOperator)) |
	name=ID ":" (hostType=STRING_S) |
	name=ID (":=" initialValue=STRING_D) ":" (hostType=STRING_S) |
	name=ID (":=" initialValue=STRING_D) ("combine" (hostType=STRING_D) "with" (hostCombineOperator=STRING_S)) | 
	name=ID ("combine" (type=ValueType) "with" (combineOperator=CombineOperator)) |
	name=ID ("combine" (hostType=STRING_S) "with" (hostCombineOperator=STRING_S))	
	)
	;
		
	
Variable returns synccharts::Variable:

    (
    name=ID |
    name=ID ":" (type=ValueType) |
    name=ID (":=" initialValue=STRING_D) |
    name=ID (":=" initialValue=STRING_D) ":" (type=ValueType) |
    name=ID ":" (hostType=STRING_S) |
    name=ID (":=" initialValue=STRING_D) ":" (hostType=STRING_S)
    )
//  name=ID
//  (":=" initialValue=STRING)?
//  (type=ValueType)?
//  ("host" hostType=STRING)?
;

Renaming returns synccharts::Renaming: 
    oldID=ID "/" newID=ID
;

		
enum ValueType returns synccharts::ValueType:
	PURE="PURE" | BOOL="BOOL" | UNSIGNED="UNSIGNED"	| 
	INTEGER="INT" | FLOAT="FLOAT" | HOST="HOST"	
;

enum CombineOperator returns synccharts::CombineOperator:
	NONE="NONE" | ADD="+" | MULT="*" | MAX="max" |
	MIN="min" | OR="or" | AND="and" | HOST="host"
;

terminal STRING_D: '"' '"' | ('"' ('\\' '"' | !'"')* ('\\' '"' | !('\\' | '"')) '"');
terminal STRING_S: "'" "'" | ("'" ('\\' "'" | !"'")* ('\\' "'" | !('\\' | "'")) "'");

