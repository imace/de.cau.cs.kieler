grammar de.cau.cs.kieler.xtext.Example with org.eclipse.xtext.common.Terminals

generate example "http://www.cau.de/cs/kieler/xtext/Example"

Model:
	(signals+=Signal)+ ';' action=Action;

Action :
	triggers+=BooleanExpression;
	
Signal:
	name = ID;
	
SignalReference:
	signal = [Signal];
	
//==============================================================================
// Expressions
//==============================================================================

// Example: not A, not false, not (A or B)
// at the latter we need the parans to indicate the right binding
NotOperation returns Expression:
	operator="not" subExpressions+=ParanthesedBooleanExpression;

// Example: either negated or normal expression (with parantheses) 
NotOrNormalExpression returns Expression:
	NotOperation | ParanthesedBooleanExpression;

// Example: A and B, not C and 42 <= ?D
AndOperation returns Expression:
	NotOrNormalExpression ({Operation.subExpressions+=current} operator="and" subExpressions+=NotOrNormalExpression)*;

// Example: A or B, C and B or D and not E 
OrOperation returns Expression:
	AndOperation ({Operation.subExpressions+=current} operator="or" subExpressions+=AndOperation)*;

// Example: true, A, 42>var1, (A or B), (not D and C or ?E = 42)
ParanthesedBooleanExpression returns Expression:
    SignalReference | '('OrOperation')' ;

// Example: not D and C or ?E = 42 or not (A and (B or C))
BooleanExpression returns Expression:
	OrOperation ;
	