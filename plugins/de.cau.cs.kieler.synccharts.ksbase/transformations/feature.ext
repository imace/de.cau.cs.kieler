import synccharts;
import utilities;

//Adds a successor to the given state 
Void addSuccessorState(State source):
 let target = new State:
 connectStates(source, target) ->
 source.parentRegion.innerStates.add(target) ->
 setSelection(target)
;

Void addSuccessorStates(List[State] states):
 states.addSuccessorState()
;


//Adds a predecessor to the given state
Void addPredecessorState(State target):
let source = new State:
connectStates(source,target) ->
target.parentRegion.innerStates.add(source) ->
(target.isInitial == true) ?
    (target.setIsInitial(false) ->
    source.setIsInitial(true)) : 
setSelection(source)
;

//Adds a choice to the given state
//by adding a conditional with two
//targets.
Void addChoice(State source):
let choice = new State:
let opt1 = new State:
let opt2 = new State:
let t1 = new Transition:
let t2 = new Transition:
choice.setLabel("C1") ->
choice.setType(StateType::CONDITIONAL) ->
opt1.setLabel("S0") ->
opt2.setLabel("S1") ->
connectStates(source,choice) ->
t1.setSourceState(choice) ->
t1.setTargetState(opt1) ->
t1.setPriority(1) ->
t2.setSourceState(choice) ->
t2.setTargetState(opt2) ->
t2.setPriority(2) ->
source.parentRegion.innerStates.add(choice) ->
source.parentRegion.innerStates.add(opt1) ->
source.parentRegion.innerStates.add(opt2)
;

//Flips source and target of the 
//given transition.
Void flipTransition(Transition t):
 let source = t.sourceState:
 let target = t.targetState: 
 t.setSourceState(target) ->
 t.setTargetState(source) ->
 setSelection(t) 
;
//Inserts a state in a transition
Void insertState(Transition t):
 let source = t.sourceState:
 let target = t.targetState:
 let newState = new State:
 t.setTargetState(newState) ->
 connectStates(newState, target) ->
 source.parentRegion.innerStates.add(newState) ->
 setSelection(newState)
;
//Connects two states
Void connectStates(State source, State target):
let transition = new Transition:
transition.setSourceState(source) ->
transition.setTargetState(target) ->
setSelection(transition)
;

// create a self loop of one state
Void addSelfLoop(State state):
  connectStates(state, state)
;

//Reroutes the target of the given transition
//to the given state
Void rerouteTransitionTarget(Transition t, State target):
 t.setTargetState(target)
;

Void rerouteTransitionTarget(State target, Transition t):
 rerouteTransitionTarget(t, target)
;

//Reroutes the source of the given transition
//to the given state
Void rerouteTransitionSource(Transition t, State source):
 //t.sourceState.outgoingTransitions.remove(t) ->
 t.setSourceState(source) //->
 //source.outgoingTransitions.add(t)
;

Void rerouteTransitionSource(State source, Transition t):
 rerouteTransitionSource(t, source)
;

//Adds a parallel region to the given state
Void upgradeState(State parentState):
let region = new Region:
let state = new State:
state.setIsInitial(true) ->
state.setLabel("Initial") ->
region.innerStates.add(state) ->
parentState.regions.add(region) ->
setSelection(state)
;

Void downgradeState(State parentState):
if parentState.regions.size > 0 then
 parentState.regions.removeAll(parentState.regions) ->
 parentState.signals.removeAll(parentState.signals)
;

//Returns true if the state is a complex state, 
//i.e. it has at least one inner region
Boolean isRegionState(State state):
    state.regions.size > 0
;

//Sets the object that should be selected after the transformation is executed
Void setSelection(Object object):
 JAVA de.cau.cs.kieler.ksbase.ui.utils.TransformationUtils.setPostTransformationSelection(java.lang.Object)
;

//Checks if the state is the root state.
Boolean isNoRootState(State state): 
    state.parentRegion.parentState != null
;

Boolean isNoRootState(State s1, State s2):
	s1.isNoRootState() && s2.isNoRootState()
;

Boolean statesOnSameHierarchy(State s1, State s2):
	s1.parentRegion == s2.parentRegion
;

Boolean isNoRootState(List[State] states):
    states.notExists(e|e.parentRegion.parentState == null)
;
// Add a state to the parent state and add all Regions
// to that new State
Void encapsulateRegions(State parentState):
  let regions = parentState.regions:
  let newRegion = new Region:
  let newState = new State:
  (parentState.regions.size > 0) ? 
    (newState.regions.addAll(regions) ->
    parentState.regions.add(newRegion) ->
    newRegion.innerStates.add(newState)) : 
   null
;

// Delete the state and move all its regions to the
// parent state 
Void flattenState(State state):
let parentRegion = state.parentRegion:
let parentState = parentRegion.parentState:
let stateSize = parentRegion.innerStates.size:
parentState.regions.addAll(state.regions) ->
state.setParentRegion(null) ->
(stateSize <= 1) ?
   parentState.regions.remove(parentRegion) : 
null
;

/////////////////////////
//     Templates      ///
/////////////////////////

//Creates a default StateChart
Void createDefault(Region rootRegion):
let state = new State:
let innerState = new State:
let region = new Region:
state.setLabel(getFileNameOfActiveEditor()) ->
state.regions.add(region) ->
innerState.setLabel("Initial") ->
innerState.setIsInitial(true) -> 
region.innerStates.add(innerState) ->
rootRegion.innerStates.add(state) ->
setSelection(innerState)
;

String getFileNameOfActiveEditor():
JAVA de.cau.cs.kieler.ksbase.ui.utils.TransformationUtils.getFileNameOfActiveEditor()
;

//Adds an initial state to a region
Void addInitialState(Region target):
 let parent = target.parentState :
 let state = new State:
 (parent != null) ?
 (state.setLabel("Initial") ->
 state.setIsInitial(true) ->
 target.innerStates.add(state) ->
 setSelection(state)) :
 createDefault(target)
;

//Good ol' ABRO
Void addABRO(Region rootRegion):
let waitABToFinal = new Transition:
let reset = new Transition:
let ABOFinal = new State:
let wATrans = new Transition:
let wBTrans = new Transition:
let wAInitial = new State:
let wAFinal = new State:
let wBInitial = new State:
let wBFinal = new State:
let wA = new Region:
let wB = new Region:
let waitAB = new State:
let ABORegion = new Region:
let ABO = new State:
let innerRootRegion = new Region:
let sigA = new Signal:
let sigB = new Signal:
let sigR = new Signal:
let sigO = new Signal:
let root = new State:
//ABRO Root State
root.setLabel("ABRO") ->
//ABRO Signal definition:
sigA.setName("A") ->
sigA.setIsInput(true) ->
sigB.setName("B") ->
sigB.setIsInput(true) ->
sigR.setName("R") ->
sigR.setIsInput(true) ->
sigO.setName("O") ->
sigO.setIsOutput(true) ->
root.signals.add(sigA) ->
root.signals.add(sigB) ->
root.signals.add(sigR) ->
root.signals.add(sigO) ->
//Inner State ABO
ABO.setLabel("ABO") ->
ABO.setIsInitial(true) ->
//Inner state Wait A and B
waitAB.setLabel("WaitAB") ->
waitAB.setIsInitial(true) ->
//Region for 'wait for a' 
wAInitial.setLabel("wA") ->
wAInitial.setIsInitial(true) ->
wA.innerStates.add(wAInitial) ->
wAFinal.setLabel("dA") ->
wAFinal.setIsFinal(true) ->
wA.innerStates.add(wAFinal) ->
wATrans.setSourceState(wAInitial) ->
wATrans.setTargetState(wAFinal) ->
wATrans.setTriggersAndEffects("A") ->
waitAB.regions.add(wA) ->
//Region for 'wait for b'
wBInitial.setLabel("wB") ->
wBInitial.setIsInitial(true) ->
wB.innerStates.add(wBInitial) ->
wBFinal.setLabel("dB") ->
wBFinal.setIsFinal(true) ->
wB.innerStates.add(wBFinal) ->
wBTrans.setSourceState(wBInitial) ->
wBTrans.setTargetState(wBFinal) ->
wBTrans.setTriggersAndEffects("B") ->
waitAB.regions.add(wB) ->
//ABO final state
ABOFinal.setLabel("done") ->
waitABToFinal.setSourceState(waitAB) ->
waitABToFinal.setTargetState(ABOFinal) ->
waitABToFinal.setType(TransitionType::NORMALTERMINATION) ->
waitABToFinal.setTriggersAndEffects("/O") ->
ABORegion.innerStates.add(waitAB) ->
ABORegion.innerStates.add(ABOFinal) ->
ABO.regions.add(ABORegion)->
reset.setSourceState(ABO) ->
reset.setTargetState(ABO) ->
reset.setType(TransitionType::STRONGABORT) ->
reset.setTriggersAndEffects("R") ->
innerRootRegion.innerStates.add(ABO) ->
root.regions.add(innerRootRegion) ->
rootRegion.innerStates.add(root)
;


///////////////////////////////////////
// Optimization and fixing of issues //
///////////////////////////////////////

// Java escapes.
Void generateStateId(State state):
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.generateStateId(de.cau.cs.kieler.synccharts.State)
;

Void generateRegionId(Region region):
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.generateRegionId(de.cau.cs.kieler.synccharts.Region)
;

Void clearIDCache():
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.clearCache()
;

List[State] getStateList():
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.getStateList()
;

Void debug(Object object):
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.debug(java.lang.Object)
;

Boolean isEnabled(String key):
 JAVA de.cau.cs.kieler.synccharts.ksbase.util.OptimizeUtils.isEnabled(java.lang.String)
;

// runs all optimizations
Void fixAllBugs(State state):
 fixAllBugs(state.parentRegion)
;

// runs all optimizations
Void fixAllBugs(Region rootRegion):
 fixIDs(rootRegion) ->
 fixTransitionPriorities(rootRegion) ->
 fixConditionalOutgoingImmediate(rootRegion) ->
 fixDummyStates(rootRegion) ->
 fixWhiteSpaces(rootRegion) ->
 removeRedundantRegions(rootRegion)
;

// runs only selected optimizations
Void optimizeSelected(State state):
 optimizeSelected(state.parentRegion)
;

// runs only selected optimizations
Void optimizeSelected(Region rootRegion):
 (if (isEnabled("fixIDs")) then
   fixIDs(rootRegion)) ->
 (if (isEnabled("fixTransitionPriorities")) then 
   fixTransitionPriorities(rootRegion)) ->
 (if (isEnabled("fixConditionalOutgoingImmediate")) then
   fixConditionalOutgoingImmediate(rootRegion)) ->
 (if (isEnabled("removeDummyStates")) then
   fixDummyStates(rootRegion)) ->
 (if (isEnabled("removeWhiteSpaces")) then
   fixWhiteSpaces(rootRegion)) ->
 (if (isEnabled("removeDummyRegions")) then
   removeRedundantRegions(rootRegion))
;

// === begin fix priorities ===
// fix the transition priorities:
// current convention is priorities starting with 1
// older synccharts may have 0 as lowest priority


Void fixTransitionPriorities(Region rootRegion):
 rootRegion.innerStates.fixTransitionPriorities()
;

Void fixTransitionPriorities(State state):
 state.outgoingTransitions.fixPriority() ->
 state.regions.fixTransitionPriorities()
;

Void fixPriority(Transition trans):
 (trans.priority == 0 ?
  trans.sourceState.outgoingTransitions.incrementPriority()
 :
  null
 )
;

Void incrementPriority(Transition trans):
 let prio = trans.priority:
 trans.setPriority(prio + 1)
;

// === end fix priorities ===


// === begin conditional outgoing immediate ===
// transitions leaving a conditional state must be immediate

Void fixConditionalOutgoingImmediate(Region rootRegion):
 rootRegion.innerStates.fixConditionalOutgoingImmediate()
;

Void fixConditionalOutgoingImmediate(State state):
 (state.type == StateType::CONDITIONAL ?
  state.outgoingTransitions.setIsImmediate(true)
 :
  null
 ) ->
 state.regions.fixConditionalOutgoingImmediate()
;

// === end conditional outgoing immediate ===

// === begin dummy states ===
// removes redundant states

Void fixDummyStates(Region rootRegion):
 let list = getStateList():
 rootRegion.innerStates.fixDummyStatesRec(list) ->
 list.removeStateFromRegion()
;

Void fixDummyStates(State state):
 fixDummyStates(state.parentRegion)
;

Void removeStateFromRegion(State state):
 state.parentRegion.innerStates.remove(state)
;

Void fixDummyStatesRec(Region rootRegion, List[State] list):
 rootRegion.innerStates.fixDummyStatesRec(list)
;

Void fixDummyStatesRec(State state, List[State] list):
 let trans = state.outgoingTransitions:
 
 (trans.size == 1 &&
  (trans.get(0).triggersAndEffects == null || 
  trans.get(0).triggersAndEffects.matches("")) &&
  trans.get(0).isImmediate &&
  state.regions.isEmpty &&
  state.suspensionTrigger == null &&
  state.entryActions.isEmpty &&
  state.exitActions.isEmpty &&
  state.innerActions.isEmpty ?
  (replaceState(state, trans.get(0).targetState) ->
  list.add(state)) 
 :
   state.regions.fixDummyStatesRec(list)
 )
;

Void replaceState(State old, State replacement):
 let incoming = getIncomingTransitions(old):
 replacement.setIsInitial(old.isInitial) ->
 incoming.setTargetState(replacement)
;

List[Transition] getIncomingTransitions(State state):
 let parent = state.parentRegion:
 parent.innerStates.outgoingTransitions.select(e|e.targetState == state)
;

// === end dummy states ===

// === begin IDs ===
// each state and region must have a unique ID

Void fixIDs(Region rootRegion):
 fixIDsRec(rootRegion) ->
 clearIDCache()
;

Void fixIDs(State state):
 fixIDs(state.parentRegion)
;

Void fixIDsRec(Region region):
 generateRegionId(region) ->
 region.innerStates.fixIDsRec()
;

Void fixIDsRec(State state):
 state.setId(state.label) ->
 generateStateId(state) ->
 state.regions.fixIDsRec()
;

// === end IDs ===

// === begin WhiteSpaces ===
// Transition Triggers and effects should not contain whitespaces

Void fixWhiteSpaces(Region region):
 region.innerStates.fixWhiteSpaces() ->
 region
;

Void fixWhiteSpaces(State state):
 state.outgoingTransitions.fixWhiteSpacesInAction() ->
 state.entryActions.fixWhiteSpacesInAction() ->
 state.exitActions.fixWhiteSpacesInAction() ->
 state.suspensionTrigger.fixWhiteSpacesInAction() ->
 state.innerActions.fixWhiteSpacesInAction() ->
 state.regions.fixWhiteSpaces() ->
 state
;

Void fixWhiteSpacesInAction(Action action):
 action.triggersAndEffects.replaceAll(" " , "")
;

Void fixNormalTerminations(Region region):
 region.innerStates.fixNormalTerminations()
;

Void fixNormalTerminations(State state):
 state.outgoingTransitions.select(e|e.type == TransitionType::NORMALTERMINATION).fixTransitionType() ->
 state.regions.fixNormalTerminations()
;

Void fixTransitionType(Transition trans):
 (trans.sourceState.regions.isEmpty ?
  trans.setType(TransitionType::WEAKABORT)
 : 
  (removeTriggerFromString(trans) ->
  trans.setTrigger(null))
 )
;

Void removeTriggerFromString(Transition trans):
 (trans.triggersAndEffects != null ?
  ((trans.effects.isEmpty ?
   trans.setTriggersAndEffects(null)
  :
   trans.setTriggersAndEffects("/" + trans.triggersAndEffects.split("/").get(1)) 
  ))
 :
  null
 )
;

// === end WhiteSpaces ===

// === begin Dummy Regions ===
// removes redundant regions

Void removeRedundantRegions(Region rootRegion):
 rootRegion.innerStates.recRemoveRedundantRegions()
;

Void recRemoveRedundantRegions(State state):
 state.regions.recRemoveRedundantRegions()
;

Void recRemoveRedundantRegions(Region region):
 if (region.innerStates.isEmpty) then
   (if (region.signals.isEmpty &&
       region.variables.isEmpty) then
       region.parentState.regions.remove(region))
   else
     region.innerStates.recRemoveRedundantRegions()
;

// === end Dummy Regions ===
