import dataflow;

//Simply connects two ports
//by adding a connection
Void connectPorts(OutputPort out, InputPort in):
 let con = new Connection:
 con.setSourcePort(out) ->
 con.setTargetPort(in) ->
 out.parentBox.connections.add(con)
;

//Connects two boxes
//If any of the boxes has an unused port, the connection is using this port
Void connectBoxes(Box source, Box target):
 let out = new OutputPort:
 let in = new InputPort:
 let sourcePorts = source.outputs.select(e|  !source.connections.exists(c|c.sourcePort == e) ) :
 //hint: this may lead to a bad performance in larger diagrams!
 let targetPorts = target.inputs.select( in | !((Box)source.eContainer).boxes.connections.exists(c| c.targetPort == in)):
 
 if (sourcePorts.size == 0 ) then {
 	source.outputs.add(out) ->
 	if (targetPorts.size == 0) then {
 		target.inputs.add(in) ->
 		target.inputs.add(in) ->
		connectPorts(out,in)	 
	}
	else {
		connectPorts(out,targetPorts.first())
	}
 } else {
 	if (targetPorts.size == 0) then {
 		target.inputs.add(in) ->
 		connectPorts(sourcePorts.first(),in)
 	}
 	else {
 		connectPorts(sourcePorts.first(),targetPorts.first())
 	}
 }
;

//Connects an output port to this first input port
//given. This method is called by ConnectAllBoxPorts
Void connectOutputsToInputs(OutputPort out,List[InputPort] in):
if (in.size > 0) then {
 out.connectPorts(in.first())->
 in.remove(in.first())
 }
;


//Connects all open output ports of the source box
//to all open input ports of the target box.
//No ports are created here, if one of the boxes has
//more empty ports they won't be touched.
//Since there is no enumeration of ports in a 
//Box, this will mess up the source<->target port order
Void connectAllBoxPorts(Box source, Box target):
 let sourceList = source.outputs.select(e| !source.connections.exists(c|c.sourcePort == e)):
 //hint: this may lead to a bad performance in larger diagrams!
 let targetList = target.inputs.select( in | !((Box)source.eContainer).boxes.connections.exists(c| c.targetPort == in)):
 sourceList.connectOutputsToInputs(targetList)
;

//Creates a successor box to the source box
Void createSuccessor(Box source):
 let target = new Box:
 connectBoxes(source,target) ->
 ((DataflowModel)source.eContainer).boxes.add(target)
;

//Creates a predecessor box to the target box
Void createPredecessor(Box target):
 let source = new Box:
 connectBoxes(source,target) ->
 ((DataflowModel)target.eContainer).boxes.add(source)
;

//Splits the given connection an inserts a new box in
//between source and target. 
Void insertBoxInConnection(Connection source):
let box = new Box:
let con = new Connection:
let tempPort = source.targetPort:
let inP = new InputPort:
let outP = new OutputPort:
box.inputs.add(inP) ->
box.outputs.add(outP) ->
source.setTargetPort(inP) ->
con.setSourcePort(outP) ->
con.setTargetPort(tempPort) ->
//Carefull here: this wont work with hierarchy, cause
//the eContainer is not a DataflowModel but a Box
((DataflowModel)source.sourcePort.eContainer.eContainer).boxes.add(box) ->
box.connections.add(con)
;