grammar de.cau.cs.kieler.synccharts.text.kits.Kits with de.cau.cs.kieler.synccharts.text.actions.Actions

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/de.cau.cs.kieler.core.annotations/model/annotations.ecore" as annotations
import "platform:/resource/de.cau.cs.kieler.core.expressions/model/expressions.ecore" as expressions 
import "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.ecore" as synccharts
import "platform:/resource/de.cau.cs.kieler.synccharts.text/model/syncchartsExtended.ecore" as syncchartsExtended


RootRegion returns synccharts::Region:
    (annotations+=ImportAnnotation)*
    (annotations += StringAnnotation)*
    ('region' (id=ID)? (label=STRING)? ':')?
    (variables+=Variable|signals+=Signal)*
    (states+=State)*;

// ---------------------------------------------------------------------------------------------------

SingleRegion returns synccharts::Region:
      (annotations += StringAnnotation)*
      ('region' (id=ID)? (label=STRING)? ':'
       (variables+=Variable|signals+=Signal)*)?
      (states+=State)*;

// ---------------------------------------------------------------------------------------------------

Region returns synccharts::Region:
    (annotations += StringAnnotation)*
    'region' (id=ID)? (label=STRING)? ':'
    (variables+=Variable|signals+=Signal)*
    (states+=State)+
    ;

// ---------------------------------------------------------------------------------------------------

State returns synccharts::State:
    (annotations += StringAnnotation)*
    (
      ((isInitial?='init') (isFinal?='final')?)
     |
      ((isFinal?='final') (isInitial?='init')?)
     )?
  
    (type=StateType)? ('state') (id=ID) (label=STRING)?
  
    (
      ('@' bodyReference = [synccharts::State|ID])
     | 
      ('{'
          (
            (signals+=Signal                |
             variables+=Variable            |
             'onentry' entryActions+=Action |
             'oninner' innerActions+=Action |
             'onexit'  exitActions+=Action  |
             'suspension' suspensionTrigger=Action)*

            (
             (regions+=SingleRegion)(regions+=Region)*
             )?
            )
          '}') 
     )?
    (outgoingTransitions+=Transition)*;

// ---------------------------------------------------------------------------------------------------

Transition returns synccharts::Transition:
    (annotations += StringAnnotation)*
	type=TransitionType	targetState=[synccharts::State|ID] ('<'priority=INT'>')? 
	( 'with'
	 (
	   ( (isImmediate?='#')? (delay=INT)? (trigger=BooleanExpression)? ('/' effects+=Effect (','effects+=Effect)*)? )
	  |
	   ( label=STRING )
	  )
	 )? 	
	(isHistory?='history')?;

// ---------------------------------------------------------------------------------------------------

Signal returns expressions::Signal:
    (annotations += StringAnnotation)*
    (isInput?='input')?
	(isOutput?='output')?
	'signal' name=ID (':=' initialValue=AnyType)?
    ((':' (type = ValueType | hostType = STRING))
      |
     (':' 'combine' (type = ValueType | hostType = STRING) 'with'
        (combineOperator = CombineOperator | hostCombineOperator = STRING))
    )?;

// ---------------------------------------------------------------------------------------------------

Variable returns expressions::Variable:
    (annotations += StringAnnotation)*
    'var' name=ID (':=' initialValue=AnyType)? ':' (type = ValueType | hostType = STRING);

// ---------------------------------------------------------------------------------------------------

enum StateType returns synccharts::StateType:
	NORMAL = 'normal' | CONDITIONAL = 'conditional' | REFERENCE = 'reference' | TEXTUAL = 'textual';

enum TransitionType returns synccharts::TransitionType:
	WEAKABORT = '-->' | STRONGABORT = 'o->' | NORMALTERMINATION = '>->';


// custom terminal rule allowing to save transition label string as they are
//terminal TRANSITION_LABEL returns ecore::EString: 
//    "%" -> "%";
    