grammar de.cau.cs.kieler.synccharts.text.kits.Kits with de.cau.cs.kieler.synccharts.text.actions.Actions

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/de.cau.cs.kieler.core.annotations/model/annotations.ecore" as annotations
import "platform:/resource/de.cau.cs.kieler.core.expressions/model/expressions.ecore" as expressions 
import "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.ecore" as synccharts

//Region returns synccharts::Region:
//  ( 
//    ('region' (label=ID))? // region ID's are introduced by the keyword "region".       
////     (variables+=Variable|signals+=Signal)* 
//     (states+=State) 
//  );
//
//// ---------------------------------------------------------------------------------------------------
  
//Nested
Region returns synccharts::Region:
  ('region' (id=ID)? (label=EString) (variables+=Variable|signals+=Signal)* (states+=State)*)
  | (states+=State)+
  ;

// ---------------------------------------------------------------------------------------------------
//
//UnnamedRegion returns synccharts::Region:
//	(states+=State)+;
//
// ---------------------------------------------------------------------------------------------------

//terminal ML_COMMENT	: '/***' -> '*/';

terminal T_ANNOTATION returns ecore::EString: 
			"/+" -> "+/";

StringAnnotation returns annotations::Annotation:
	{annotations::StringAnnotation} value=T_ANNOTATION
    ;

State returns synccharts::State:
//  ( annotations += StringAnnotation )*
  (
    ((isInitial?='init') (isFinal?='final')?)
   |
    ((isFinal?='final') (isInitial?='init')?)
   )?
  
  (type=StateType)? ('state')? (id=ID)? (label=EString)
  
  (
    ( (outgoingTransitions+=Transition ("," outgoingTransitions+=Transition)* )? ';')
   | 
    ('{'
        (
          (signals+=Signal                |
           variables+=Variable            |
           'onentry' entryActions+=Action |
           'oninner' innerActions+=Action |
           'onexit'  exitActions+=Action  |
           'suspension' suspensionTrigger=Action)*
           
          (regions+=Region ('||' regions+=Region)*) )
           
        '}' (outgoingTransitions+=Transition ("," outgoingTransitions+=Transition)* ';')?
     ) 
   );

// ---------------------------------------------------------------------------------------------------

Variable returns expressions::Variable:
  'var' name=ID ((':=' initialValue=IntegerOrString)? ':' type=ValueType) ";";

// ---------------------------------------------------------------------------------------------------

IntegerOrString :
   INT | EString;

// ---------------------------------------------------------------------------------------------------

Signal returns expressions::Signal:
(isInput?='input')?
	(isOutput?='output')?
	'signal'
	(name=EString)
	(':=' initialValue=EString)?
    ((':' type=ValueType)
      |
     (':' 'combine' type=ValueType 'with' (combineOperator=CombineOperator|hostCombineOperator=EString) )
    )? ";";

// ---------------------------------------------------------------------------------------------------

Transition returns synccharts::Transition:
	type=TransitionType 
	targetState=[synccharts::State|EString]
	('<'priority=INT'>')? 
	('with' (isImmediate?='#')? ("delay:=" delay=INT)? 
	  ( trigger=BooleanExpression)?
      ('/' effects+=Effect (','effects+=Effect)*)?
	)? 
	(isHistory?='history')? (label=STRING)?;

enum StateType returns synccharts::StateType:
	NORMAL = 'normal' | CONDITIONAL = 'conditional' | REFERENCE = 'reference' | TEXTUAL = 'textual';

enum TransitionType returns synccharts::TransitionType:
	WEAKABORT = '-->' | STRONGABORT = 'o->' | NORMALTERMINATION = '>->';

