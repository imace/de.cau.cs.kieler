grammar de.cau.cs.kieler.sim.esi.Esi
//with org.eclipse.xtext.common.Terminals
hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate esi "http://www.cau.de/cs/kieler/sim/esi/Esi"

tracelist:
    (traces+=trace)+;

trace:
    '!' 'reset' ';'
    (ticks+=tick)+;

tick:
    {tick}
    (input+=signal)*
    ('%' 'Output' ':' (output+=signal)*)?
    (extraInfos+=kvpair)*
    ';';

/* From the Xtext documentation:
 * The ?= sign (boolean assignment operator) expects a feature of type EBoolean and sets it to true if
 * the right hand side was consumed independently from the concrete value of the right hand side.
 * 
 * I. e. 'valued' is set to true if a value could be read.
 */
signal:
    name=(AlphaNumSpecial) // | AlphaNumSpecial)
    (valued?='('
    val=(value)
    ')')?;

value:
    (EsoInt | EsoFloat | EsoBool | EsoString)
;

EsoInt returns ecore::EInt:
    Int
;

EsoString returns ecore::EString:
    STRING
;

EsoFloat returns ecore::EFloat:
    Float
;

EsoBool returns ecore::EBoolean:
    'true' | 'false'
;

kvpair:
    '%%'
    key=(AlphaNumSpecial) // | AlphaNumSpecial)
    ':'
    value=(AlphaNumSpecial | Int | Float);

terminal Special:
    ',' | '.' | '/' | '@' | '#' | '$' | '&' | '*' | '=' | '+' | '-';
    
terminal AlphaNumSpecial:
('a'..'z' | 'A'..'Z' | Special) ('a'..'z' | 'A'..'Z' | '0'..'9' | Special)* ;

terminal STRING : 
            '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
            "'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
        ; 

//terminal Alpha:
//    ('a'..'z' | 'A'..'Z')+;

terminal Int:
    ('+' | '-')? ('0'..'9')+
;

terminal Float:
    ('+' | '-')? ('0'..'9')+ '.' ('0'..'9')+;
    
//terminal AlphaNum:
//    (Alpha | '0'..'9')+;




    
//terminal Comment:
//    '%' ('a'..'z' | 'A'..'N' | 'P'..'Z' | ('0' .. '9') | Special)->'\n';

terminal WS:
    '\t' | ' ' | '\r' | '\n';


