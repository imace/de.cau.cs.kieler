grammar de.cau.cs.kieler.s.S with org.eclipse.xtext.common.Terminals

//import 'http://kieler.cs.cau.de/annotations' as annotations

generate s "http://www.cau.de/cs/kieler/s"

Program :
	'Synchronous Program' name = ID '(' priority=IntValue ')'
	('Inputs' (signals += Signal) (',' signals += Signal)*';')?
	('Outputs' (signals += Signal) (',' signals += Signal)*';')?
	('Signals' (signals += Signal) (',' signals += Signal)*';')?
	(states += State)+
;

Annotation:
	'@' key = ID ':'
	value = STRING
;

Annotatable:
	State | Signal | Instruction;


State:
	name = ID ':'
	((annotations += Annotation) (',' annotations += Annotation)*)?
	('SIGNALS' (signals+=Signal)* ';')? 
	(instructions+=Instruction';')*
;

Signal:
	name=ID ':' 
	type = SignalType
	('combine' combineFunction = [CombineFunction])?
	((annotations += Annotation) (',' annotations += Annotation)*)?
;

CombineFunction:
	name=ID
	type=SignalType
;


SignalType:
	PrimitiveSignalType | CustomSignalType;
  
CustomSignalType:
  'CUSTOM' customType = STRING;

enum PrimitiveType:
	Pure='PURE' | Int='INT' | Bool='BOOL' | Float='FLOAT';
	
PrimitiveSignalType:
	primitiveType = PrimitiveType
	;
  
IntValue: 
	value=INT;
FloatValue:
    value=Float; 
BooleanValue:
    value=Boolean;
    
terminal Float: 
	((INT"."INT | INT("."INT)?("e"|"E")("+")?INT))"f"? | INT"f";
terminal Boolean: 
	"true" | "false";
	

//---------------------------------------------------------------------
//----------------------- List of S-Instructions ----------------------
//---------------------------------------------------------------------
Instruction:
   Halt | Abort | Join | Pause | Term | Present | Goto | Fork | Forke | Emit | Await | Prio
   ((annotations += Annotation) (',' annotations += Annotation)*)?
   ;

Pause :
	{Pause} 
	'PAUSE''('continuation=[State]?')'
  ;
  
Term : 
	{Term}
	'TERM''('continuation=[State]?')'
  ;
  
Halt : 
	{Halt}
	'Halt''('continuation=[State]?')'
  ;
  
Join : 
	{Join}
	'JOIN''('continuation=[State]?')'
  ;
  
Abort : 
	{Abort}
	'ABORT''('continuation=[State]?')'
  ;
  
Present : 
	'PRESENT''('signal = [Signal] (',' continuation=[State])?')' '{'
		(instructions += Instruction';')*
	'}'
  ;
  
Goto :
	'GOTO''('state=[State] (',' continuation=[State])?')'
  ;
  
Fork :
	'FORK''('state=[State] ',' priority=IntValue (',' continuation=[State])?')'
  ;
  
Forke :
	'FORKE''('state=[State] (',' continuation=[State])?')'
  ;
  
Emit:
	'EMIT''('signal= [Signal] (',' continuation=[State])?')'
;

Await:
	'AWAIT''('signal= [Signal] (',' continuation=[State])?')'
;

Prio:
	'PRIO''('priority=IntValue (',' continuation=[State])?')'
;