import synccharts;

context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not have a name!" :
	(name == null) || (name.length == 0);

context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not be initial or final!" :
	stateFlag == StateFlag::NORMAL;
	
context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not contain regions!" :
	(regions == null) || (regions.size == 0);
	
context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not contain signals!" :
	(signals == null) || (signals.size == 0);
	
context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not contain variables!" :
	(variables == null) || (variables.size == 0);
	
context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not contain actions!" :
	((entryActions == null) || (entryActions.size == 0))
	&& ((innerActions == null) || (innerActions.size == 0))
	&& ((exitActions == null) || (exitActions.size == 0));
	
context State if (stateKind == StateKind::PSEUDO) ERROR "pseudo states must not contain a suspension trigger!" :
	suspensionTrigger == null;
	
context State if (stateKind != StateKind::REFERENCE) ERROR "only reference states may contain signal renamings!" :
	(signalRenamings == null) || (signalRenamings.size == 0);

context Signal if (isLocal) ERROR "local signals must not be input or output!" :
	((!isInput) && (!isOutput));
	
context Signal ERROR "signals have to be input, local or output!" :
	isInput || isLocal || isOutput;

context Signal if ((initialValue == null) || (initialValue == "")) ERROR "only valued signals may have a combine type other than NONE!" :
	combineType == CombineType::NONE;

context Transition if (immediate) ERROR "immediate transitions must not have a delay!" :
	(delay == null) || (delay == 1);
	
context Transition ERROR "Delays have to be at least 1!" :
	(delay == null) || (delay >= 1);
	
context SignalRenaming ERROR "The old and new signal of a signal renaming must be different!" :
	oldSignal != newSignal;
	
context Operator if ((operatorKind == OperatorKind::NOT) || (operatorKind == OperatorKind::VAL)) ERROR "Unary operators have one operand!" :
	subExpressions.size == 1;
	
context Operator if (!((operatorKind == OperatorKind::NOT) || (operatorKind == OperatorKind::VAL))) ERROR "Binary operators have two operands!" :
	subExpressions.size == 2;
	
context Emission if (signal.type == ValueType::PURE) ERROR "Pure signals cannot be assigned a value!" :
	newValue == null;
	
context Emission if (!(signal.type == ValueType::PURE)) ERROR "Valued signals must be assigned a value!" :
	newValue != null;